ACTOR D4Shotgun : BrutalD464Weapon Replaces ShotgunSpawner
{
Weapon.SelectionOrder 1300
Weapon.AmmoUse 1
Weapon.AmmoGive 8
Weapon.AmmoType "Shell"
Inventory.PickupSound "Shotgun/Pickup"
Inventory.PickupMessage "You got the Combat Shotgun!"
Obituary "$OB_MPSHOTGUN"
Tag "$TAG_SHOTGUN"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
+inventory.IGNORESKILL
	States
	{
	Spawn:
		D4W3 A -1
		Stop
	KickingFlash:
		SH2G A 0 A_JumpIfInventory("ExplosiveShotgun",1,3)	
		SH1G A 0 A_JumpIfInventory("BurstFireShotgun",1,2)
		SHTG A 0
		"####" A 18 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop
	SlideKickingStart:
		SH2G A 0 A_JumpIfInventory("ExplosiveShotgun",1,3)	
		SH1G A 0 A_JumpIfInventory("BurstFireShotgun",1,2)
		SHTG A 0
		"####" A 20 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	SlideKickingEnd:
		"####" A 6 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop
	v1ready:
	Ready:
		SH2G A 0 A_JumpIfInventory("ExplosiveShotgun",1,3)	
		SH1G A 0 A_JumpIfInventory("BurstFireShotgun",1,2)
		SHTG A 0
		"####" A 1 A_WeaponReady
		TNT1 A 0 ACS_NamedExecuteAlways("ShotgunUpgradeCooldown")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("WeaponAltMode",1,"ChangeMode")
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		Loop
	ChangeMode:
		TNT1 A 0 A_TakeInventory("WeaponAltMode",1)
		TNT1 A 0 A_JumpIfInventory("ExplosiveShotgun",1,"BurstUpgradeCheck")		
		TNT1 A 0 A_JumpIfInventory("BurstFireShotgun",1,"ExplosiveUpgradeCheck")
		TNT1 A 0 A_JumpIfInventory("ExplosiveMode",1,"GoToUpgrade")
		TNT1 A 0 A_JumpIfInventory("BurstMode",1,1)
		Goto AboutUpgrade
	GoToUpgrade:
		SH2S A 0 A_JumpIfInventory("ExplosiveShotgun",1,3)	
		SH1S A 0 A_JumpIfInventory("BurstFireShotgun",1,2)
		SHTS A 0
		"####" DCBA 1
		TNT1 A 3
		TNT1 A 0 A_JumpIfInventory("ExplosiveShotgun",1,2)
		TNT1 A 0 A_JumpIfInventory("ExplosiveMode",1,"ExplosiveUpgrade")
	GrenadeUpgrade:
		TNT1 A 0 A_Print("Burst Fire Mode Selected",2)
		SH1B ABCDEFFF 1
		SH1B GHI 1
		SH1B I 1 Offset(1, 33) A_PlaySound("Shotgun/SwitchToBurstFire",5)
		TNT1 A 0 A_JumpIfInventory("ExplosiveShotgun",1,"FromExplodeToBurst")		
		SH1B I 1 Offset(3, 35)
		SH1B I 1 Offset(4, 36) A_GiveInventory("BurstFireShotgun",1)
		SH1B I 1 Offset(2, 34)
		SH1B I 1 Offset(1, 33)
		SH1B JKL 1
		Goto Ready
	FromExplodeToBurst:
		SH1B I 1 Offset(3, 35) A_TakeInventory("ExplosiveShotgun",1)
		SH1B I 1 Offset(4, 36) A_GiveInventory("BurstFireShotgun",1)
		SH1B I 1 Offset(2, 34)
		SH1B I 1 Offset(1, 33)
		SH1B JKL 1
		Goto Ready
	ExplosiveUpgradeCheck:
		TNT1 A 0 A_JumpIfInventory("ExplosiveMode",1,"GoToUpgrade")
		Goto OneUpgradeOnly
	BurstUpgradeCheck:
		TNT1 A 0 A_JumpIfInventory("BurstMode",1,"GoToUpgrade")
		Goto OneUpgradeOnly
	ExplosiveUpgrade:
		TNT1 A 0 A_Print("Explosive Mode Selected",2)
		SH2A ABCDEFGG 1
		TNT1 A 0 A_PlaySound("Shotgun/SwitchToExplosive",5)
		SH2A HIJKL 1
		TNT1 A 0 A_JumpIfInventory("BurstFireShotgun",1,"FromBurstToExplode")
		SH2G A 1 Offset(6, 38) A_GiveInventory("ExplosiveShotgun",1)
		SH2G A 1 Offset(4, 36)
		SH2G A 1 Offset(3, 35)
		SH2G A 1 Offset(2, 34) 
		SH2G A 1 Offset(1, 33)
		SH2G A 1 Offset(0, 32)
		Goto Ready
	FromBurstToExplode:
		SH2G A 1 Offset(6, 38) A_TakeInventory("BurstFireShotgun",1)
		SH2G A 1 Offset(4, 36) A_GiveInventory("ExplosiveShotgun",1)
		SH2G A 1 Offset(3, 35)
		SH2G A 1 Offset(2, 34) 
		SH2G A 1 Offset(1, 33)
		SH2G A 1 Offset(0, 32)
		Goto Ready
	Deselect:
		SH2G A 0 A_JumpIfInventory("ExplosiveShotgun",1,3)
		SH1G A 0 A_JumpIfInventory("BurstFireShotgun",1,2)
		SHTG A 0
		"####" A 1 Offset(-8, 37)
		"####" A 1 Offset(-20, 43)
		"####" A 1 Offset(-45, 55)
		"####" A 1 Offset(-70, 68)
		TNT1 A 0 A_TakeInventory("IsD4Weapon",0)
		TNT1 A 0 A_TakeInventory("D4ShotgunSelected",0)
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_GiveInventory("D4ShotgunSelected",1)
		TNT1 A 0 A_GiveInventory("IsD4Weapon",1)
		SH2G A 0 A_JumpIfInventory("ExplosiveShotgun",1,3)
		SH1G A 0 A_JumpIfInventory("BurstFireShotgun",1,2)
		SHTG A 0
		"####" A 1 Offset(75, 75)
		"####" A 1 Offset(50, 60)
		"####" A 1 Offset(28, 45)
		"####" A 1 Offset(10, 34)
		TNT1 A 0 Offset(0,32) A_Raise
		Wait
	Fire:
		TNT1 A 0 ACS_NamedExecuteAlways("ShotgunUpgradeCooldown")
		TNT1 AAAAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,6,-6,0,random(-3,3))
		TNT1 A 0 A_FireBullets(4.2,3.9,7,7,"ShotgunPuff")
		TNT1 A 0 A_JumpIfInventory("ExplosiveActivated",1,"ExplosiveFire")
		TNT1 A 0 A_JumpIfInventory("BurstFireActivated",1,"BurstFire")
		TNT1 A 0 A_PlaySound("D2K16Shotgun/Fire",1)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		SH2F A 0 A_JumpIfInventory("ExplosiveShotgun",1,3)
		SH1F A 0 A_JumpIfInventory("BurstFireShotgun",1,2)
		SHTF A 0
		"####" A 1 BRIGHT A_SetAngle(random(4,-4) + angle)
		"####" B 1 BRIGHT 
		"####" CCCC 1 A_SetAngle(random(1,-1) + angle)
		"####" DEFGHI 1
		SH2G A 0 A_JumpIfInventory("ExplosiveShotgun",1,3)
		SH1G A 0 A_JumpIfInventory("BurstFireShotgun",1,2)
		SHTG A 0
		"####" A 3 //A_SetAngle(random(1,-1) + angle)
	Pump:
		SH2G A 0 A_JumpIfInventory("ExplosiveShotgun",1,3)
		SH1G A 0 A_JumpIfInventory("BurstFireShotgun",1,2)
		SHTG A 0	
		"####" BCD 1
		"####" A 0 A_PlaySound("D2K16Shotgun/PumpIn",7)
		"####" A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,17,-15)
		"####" EFGGGGFE 1
		"####" A 0 A_PlaySound("D2K16Shotgun/PumpOut",7)
		"####" DCB 1
		"####" A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		"####" A 3
		"####" A 0 A_Refire
		"####" A 5 A_WeaponReady
		Goto Ready
	BurstFire:
		TNT1 A 0 A_PlaySound("D2K16Shotgun/BurstFire",1)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		SH1A L 0 A_JumpIfInventory("BurstFiredSMG",2,3)
		SH1A I 0 A_JumpIfInventory("BurstFiredSMG",1,2)
		SH1A F 0
		SH1A "#" 1 BRIGHT A_SetAngle(random(4,-4) + angle)
		SH1A M 0 A_JumpIfInventory("BurstFiredSMG",2,3)
		SH1A J 0 A_JumpIfInventory("BurstFiredSMG",1,2)
		SH1A G 0		
		SH1A "#" 1 BRIGHT
		SH1A B 0 A_JumpIfInventory("BurstFiredSMG",2,3)
		SH1A K 0 A_JumpIfInventory("BurstFiredSMG",1,2)		
		SH1A H 0
		SH1A H 1 A_GiveInventory("BurstFiredSMG",1)
		TNT1 A 0 A_JumpIfInventory("BurstFiredSMG",3,1)
		Goto Fire
		TNT1 A 0 ACS_NamedExecute("BurstFireShotgunStartCooldown")
		TNT1 A 0 A_TakeInventory("BurstFiredSMG",0)
		TNT1 A 0 A_ZoomFactor(1.00)		
		SH1F GHI 1
		SH1G A 1	
		SH1G BCD 1
		SH1G A 0 A_PlaySound("D2K16Shotgun/PumpIn",7)
		SH1G AAA 0 A_FireCustomMissile("ShotCaseSpawn",0,0,17,-15)
		SH1G EFGGGGFE 1
		SH1G A 0 A_PlaySound("D2K16Shotgun/PumpOut",7)
		SH1G DCB 1
		SH1G A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		SH1G A 3
		SH1G A 0 A_Refire
		SH1G A 5 A_WeaponReady
		Goto Ready
	ExplosiveFire:
		TNT1 A 0 A_PlaySound("D2K16Shotgun/GrenadeFire",1)
		TNT1 A 0 A_FireCustomMissile("ShotgunGrenade",random(-1,1),0,6,-2,0,random(-1,1))
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		SH2B C 1 BRIGHT A_SetAngle(random(4,-4) + angle)
		SH2F B 1 BRIGHT	ACS_NamedExecute("ExplosiveShotgunStartCooldown")
		TNT1 A 0 A_ZoomFactor(1.00)		
		SH2F FGHI 1 A_SetAngle(random(1,-1) + angle)
		SH2G A 1
		Goto Pump
	AltFire:
		TNT1 A 0 A_JumpIfInventory("ExplosiveShotgun",1,"ExplosiveAltFire")
		TNT1 A 0 A_JumpIfInventory("BurstFireShotgun",1,"BurstAltFire")
		Goto AboutUpgrade
	ExplosiveAltFire:	
		TNT1 A 0 A_JumpIfInventory("ExplosiveCooldown",1,"IsCooldown")
		TNT1 A 0 A_ZoomFactor(1.25)
		SH2B AB 1
		TNT1 A 0 A_GiveInventory("ExplosiveActivated",1)
		Goto AltHold
	BurstAltFire:
		TNT1 A 0 A_JumpIfInventory("BurstFireCooldown",1,"IsCooldown")
		TNT1 A 0 A_ZoomFactor(1.25)
		SH1A ABBBB 1
		TNT1 A 0 A_PlaySound("Shotgun/TripleShotAim",5)
		SH1A C 2
		SH1A D 2
		SH1A E 2
		TNT1 A 0 A_GiveInventory("BurstFireActivated",1)
	AltHold:
		SH2B B 0 A_JumpIfInventory("ExplosiveShotgun",1,2)
		SH1A E 0
		"####" "#" 1 A_WeaponReady(WRF_NOSECONDARY)
		TNT1 A 0 A_Refire
		TNT1 A 0 A_JumpIfInventory("ExplosiveShotgun",1,"ExplosiveNoAltFire")
	BurstNoAltFire:
		TNT1 A 0 A_TakeInventory("BurstFireActivated",1)
		TNT1 A 0 A_ZoomFactor(1.00)
		SH1A BA 1
		Goto Ready
	ExplosiveNoAltFire:
		TNT1 A 0 A_TakeInventory("ExplosiveActivated",1)
		TNT1 A 0 A_ZoomFactor(1.00)
		SH2B BA 1
		Goto Ready		
	NoAmmo:
		TNT1 A 0 A_Print("You don't have enough ammo to use this weapon's alt fire",2)
		Goto Ready
	}
}

ACTOR ShotgunGrenade : 64RevenantTracer3
{
Scale 0.5
Damage 10
Speed 40
Radius 4
Height 4
Gravity 0.4
Renderstyle Add
-thruactors
SeeSound "None"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("CoolAndNewFlameTrails",0,0,random(-2,2))
		FR7X A 1 BRIGHT A_SpawnItemEx("RedFlareSmall")
		TNT1 A 0 A_CustomMissile("OldschoolRocketSmokeTrail2",2,0,random(160,210),2,random(-30,30))
		TNT1 A 0 A_SpawnItem("CoolAndNewFlameTrails")
		FR7X B 1 BRIGHT A_SpawnItemEx("RedFlareSmall")
		Loop
	Death:
		TNT1 A 0 Radius_Quake(3,8,0,15,0)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterSmall")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterSmall")
		TNT1 A 0 A_SpawnItemEx("ExplosionFlareSpawner")
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("MediumExplosionFlames",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("RocketExplosion2",0,0,13)
		TNT1 A 2
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		TNT1 A 0 A_PlaySound("FAREXPL",3)
		TNT1 A 20
		Stop
	}
}

actor RocketExplosion2
{
+MISSILE
Radius 2
Height 2
Damagetype ExplosiveImpact
	States
	{
	Spawn:
		Goto Death
	Death:
		TNT1 A 3
		TNT1 A 1 A_Explode(70,85)
	Stop
	}
}