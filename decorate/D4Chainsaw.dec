Actor D4Chainsaw : BrutalD464Weapon Replaces ChainsawSpawner
{
Scale 0.82
Weapon.SlotNumber 1
Weapon.SlotPriority 2
Weapon.SelectionOrder 2200
Weapon.Kickback 0
Inventory.PickupMessage "$GOTCHAINSAW"
Inventory.PickupSound "Painsaw/Pickup"
Weapon.UpSound "null"
Weapon.AmmoType "SawGas"
Weapon.AmmoUse 1
Weapon.AmmoGive 100
+WEAPON.MELEEWEAPON
+NOAUTOFIRE
+WEAPON.AXEBLOOD
Tag "$TAG_CHAINSAW"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	KickingFlash:
		PS4S DCBA 1
		PS4S A 8
		PS4S ABCD 1
		Stop
	AirKickingFlash:
		PS4S DCBA 1
		PS4S A 10
		PS4S ABCD 1
		Stop
	SlideKickingStart:
		PS4S DCBA 1
		PS4S A 12
	SlideKickingEnd:
		PS4S A 6
		PS4S ABCD 1
		Stop
	Spawn:
		D4W1 B -1
		Stop
	Select:
		TNT1 A 0 A_GiveInventory("D4PeaGunSelected",1)
		TNT1 A 0 A_GiveInventory("IsD4Weapon",1)
		TNT1 A 0 A_PlaySound("Painsaw/Select",CHAN_7)
		PS4S ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	v1Ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("WeaponAltMode",1,"NoAltMode")
		TNT1 A 0 A_JumpIfInventory("SawGas",1,1)
		Goto NotReady
		PS4W AB 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Painsaw/Idle",CHAN_6,1,TRUE)
		PS4W AB 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Painsaw/Idle",CHAN_6,1,TRUE)
		PS4W AB 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Painsaw/Idle",CHAN_6,1,TRUE)
		PS4W AB 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Painsaw/Idle",CHAN_6,1,TRUE)
		TNT1 A 0 A_TakeInventory("SawGas",1)
		Loop
	NotReady:
		PS4W A 5 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("WeaponAltMode",1,"NoAltMode")
		TNT1 A 0 A_JumpIfInventory("SawGas",1,"Select")
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("SawGas",1,1)
		Goto FinishFire
		PS4W AB 1
	Hold:
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_PlaySound("Painsaw/Loop",CHAN_WEAPON,1,TRUE)
	Hold1:
		PS4W C 1 A_FireCustomMissile("SawDamage",15)
		PS4W D 1 A_FireCustomMissile("SawDamage",-15,0)
		PS4W E 1 A_FireCustomMissile("SawDamage",15,0)
		PS4W F 1 A_FireCustomMissile("SawDamage",-15,0)
		TNT1 A 0 A_ReFire("Hold1")
		TNT1 A 0 A_PlaySound("Painsaw/LoopEnd",7)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
	FinishFire:
		PS4W BA 1
		Goto Ready
	Deselect:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("D4PeaGunSelected",0)
		TNT1 A 0 A_TakeInventory("IsD4Weapon",0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_PlaySound("Painsaw/DeSelect",7)
		PS4S DCBA 1
		TNT1 A 0 A_Lower
		Wait
	UpgradeWeapons:
		TNT1 A 0 A_StopSound(CHAN_6)
		TNT1 A 0 A_TakeInventory("IsUpgradingWeapons",1)
		TNT1 A 0 SetPlayerProperty(0,1,4)
		PK4P ABCDEFGH 2
		TNT1 A 6
		TNT1 A 0 A_PlaySound("UACBot/Pickup")
		TNT1 A 0 SetPlayerProperty(0,0,4)
		TNT1 A 12
		Goto Select
	NoAltMode:
		TNT1 A 0 A_TakeInventory("WeaponAltMode",1)
		TNT1 A 0 ACS_NamedExecuteWithResult("NoUpgradedWeapons",1)
		Goto Ready
	}
}
