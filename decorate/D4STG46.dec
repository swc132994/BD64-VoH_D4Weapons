Actor D4STG46 : BrutalD464Weapon replaces MP40Spawner
{
Scale 0.9
Weapon.SelectionOrder 850
Inventory.PickupSound "D4STG46/Pickup"
Weapon.AmmoType1 "Clip"
Weapon.AmmoUse1 1
Weapon.AmmoGive1 20
Weapon.AmmoType2 "RocketAmmo"
Weapon.AmmoUse2 1
Weapon.AmmoGive2 2
Inventory.PickupMessage "You got the STG-46!"
Obituary "%o was gunned down by %k."
Tag "STG-46"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.CHEATNOTWEAPON
	States
	{
	Spawn:
		D4W2 C -1
		Stop
	KickingFlash:
		STGG A 18 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop
	SlideKickingStart:
		STGG A 23 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	SlideKickingEnd:
		STGG A 6 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop
	v1ready:
	Ready:
		STGG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("WeaponAltMode",1,"NoAltMode")
		Loop
	Deselect:
		TNT1 A 0 A_TakeInventory("IsD4Weapon",0)
		TNT1 A 0 A_TakeInventory("D4PeaGunSelected",0)
		TNT1 A 0 A_TakeInventory("D4AltMode",0)
		STGG A 1 Offset(-8, 37)
		STGG A 1 Offset(-20, 43)
		STGG A 1 Offset(-45, 55)
		STGG A 1 Offset(-70, 68)		
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_GiveInventory("D4PeaGunSelected",1)
		TNT1 A 0 A_GiveInventory("IsD4Weapon",1)
		STGG A 1 Offset(75, 75)
		STGG A 1 Offset(50, 60)
		STGG A 1 Offset(28, 45)
		STGG A 1 Offset(10, 34)		
		TNT1 A 0 Offset(0,32) A_Raise
		Wait
	Fire:
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0,6)
		STGF A 1 BRIGHT
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-3,3),0,12,-9,0,random(-2,2))
		TNT1 A 0 A_FireBullets(0.0,0.0,-1,5,"HitPuff")
		TNT1 A 0 A_PlaySound("D4STG46/Fire",CHAN_WEAPON)
		STGF BC 1
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,0,10,-12)
		TNT1 A 0 A_Refire
		STGG A 1
		Goto Ready
	AltFire:
		STGG A 1 Offset(-8, 37)
		STGG A 1 Offset(-20, 43)
		STGG A 1 Offset(-45, 55)
		STGG A 1 Offset(-70, 68)
		TNT1 A 0 Offset(1,32) A_GunFlash("GrenadeThrowFlash")
		TNT1 A 35 
		TNT1 A 0 A_FireCustomMissile("64StickGrenadePlayer",0,1)
		TNT1 A 4
		STGG A 1 Offset(75, 75)
		STGG A 1 Offset(50, 60)
		STGG A 1 Offset(28, 45)
		STGG A 1 Offset(10, 34)
		TNT1 A 0 A_Refire
		Goto Ready
	GrenadeThrowFlash:
		GREN ABCD 1
		GREN EFGGHIII 1
		TNT1 A 0 A_PlaySound("GRNPIN",CHAN_WEAPON)
		GREN JKKLMNNOOOPQ 1
		GREN RSTUVWXYY 1
		GREN Z 1 A_PlaySound("GRNTOSS",CHAN_WEAPON)
		GREN "[\]" 1
		2REN ABCD 1
		TNT1 A 5
		Stop
	NoAltMode:
		TNT1 A 0 A_TakeInventory("WeaponAltMode",1)
		TNT1 A 0 ACS_NamedExecuteWithResult("NoUpgradedWeapons",6)
		Goto Ready
	}
}