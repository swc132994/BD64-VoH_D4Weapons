ACTOR D4GaussCannon : BrutalD464Weapon Replaces GaussCannonSpawner
{
Weapon.SelectionOrder 2799
Weapon.AmmoUse 15
Weapon.AmmoGive 30
Weapon.AmmoType "Cell"
Inventory.PickupMessage "You got the Gauss Cannon!"
Inventory.PickupSound "D4GaussCannon/Pickup"
Obituary "$OB_RAILGUN"
Tag "Gauss Cannon"
	States
	{
	KickingFlash:
		GA2G A 0 A_JumpIfInventory("SiegeGauss",1,3)
		GA1G A 0 A_JumpIfInventory("PreciseGauss",1,2)
		GAUG A 0
		"####" A 18 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop
	SlideKickingStart:
		GA2G A 0 A_JumpIfInventory("SiegeGauss",1,3)
		GA1G A 0 A_JumpIfInventory("PreciseGauss",1,2)
		GAUG A 0
		"####" A 20 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	SlideKickingEnd:
		"####" A 6 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop
	Spawn:
		D4W7 B -1
		Stop
	v1Ready:
	Ready:
		GA2G A 0 A_JumpIfInventory("SiegeGauss",1,3)
		GA1G A 0 A_JumpIfInventory("PreciseGauss",1,2)
		GAUG A 0
		"####" A 1 A_WeaponReady(WRF_ALLOWZOOM)
		"####" A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		"####" A 0 A_JumpIfInventory("WeaponAltMode",1,"ChangeMode")
		"####" A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		Loop
	Ready2:
		TNT1 A 0 A_GiveInventory("PreciseCharger",1)
		TNT1 A 0 A_PlaySound("D4GaussCannon/Charge2Loop",CHAN_ITEM,1,1)		
		GA1G D 1 A_WeaponReady(WRF_NOBOB)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("WeaponAltMode",1,"ChangeMode")
		Loop
	ChangeMode:
		TNT1 A 0 A_TakeInventory("WeaponAltMode",1)
		TNT1 A 0 A_JumpIfInventory("SiegeGauss",1,"PreciseUpgradeCheck")
		TNT1 A 0 A_JumpIfInventory("PreciseGauss",1,"SiegeUpgradeCheck")
		TNT1 A 0 A_JumpIfInventory("SiegeMode",1,"GoToUpgrade")
		TNT1 A 0 A_JumpIfInventory("PreciseMode",1,1)
		Goto AboutUpgrade
	GoToUpgrade:
		GA2G A 0 A_JumpIfInventory("SiegeGauss",1,3)
		GA1G A 0 A_JumpIfInventory("PreciseGauss",1,2)
		GAUG A 0
		"####" A 1 Offset(-8, 37)
		"####" A 1 Offset(-20, 43)
		"####" A 1 Offset(-45, 55)
		"####" A 1 Offset(-70, 68)
		TNT1 A 3 Offset(1, 32)
		TNT1 A 0 A_JumpIfInventory("SiegeMode",1,2)
		TNT1 A 0 A_JumpIfInventory("PreciseMode",1,"PreciseUpgrade")
	SiegeUpgradeCheck:
		TNT1 A 0 A_JumpIfInventory("PreciseMode",1,"GoToUpgrade")
		Goto OneUpgradeOnly
	PreciseUpgradeCheck:
		TNT1 A 0 A_JumpIfInventory("PreciseMode",1,"GoToUpgrade")
		Goto OneUpgradeOnly
	PreciseUpgrade:
		TNT1 A 0 A_Print("Precise Mode Selected",2)
		GA1S ABBCEGGG 1
		GA1S H 2 A_GiveInventory("PreciseGauss",1)
		GA1S IJKLMNOP 2
		Goto Ready
	Deselect:
		TNT1 A 0 A_TakeInventory("IsD4Weapon",0)
		TNT1 A 0 A_TakeInventory("D4GaussSelected",0)
		TNT1 A 0 A_TakeInventory("D4AltMode",0)
		GA2G A 0 A_JumpIfInventory("SiegeGauss",1,3)
		GA1G A 0 A_JumpIfInventory("PreciseGauss",1,2)
		GAUG A 0
		"####" A 1 Offset(-8, 37)
		"####" A 1 Offset(-20, 43)
		"####" A 1 Offset(-45, 55)
		"####" A 1 Offset(-70, 68)
		"####" A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_GiveInventory("D4GaussSelected",1)
		TNT1 A 0 A_GiveInventory("IsD4Weapon",1)
		GA2G A 0 A_JumpIfInventory("SiegeGauss",1,3)
		GA1G A 0 A_JumpIfInventory("PreciseGauss",1,2)
		GAUG A 0
		"####" A 1 Offset(75, 75)
		"####" A 1 Offset(50, 60)
		"####" A 1 Offset(28, 45)
		"####" A 1 Offset(10, 34)
		"####" A 0 Offset(0,32) A_Raise
		Wait

	Fire:
		TNT1 A 0 A_PlaySound("D4GaussCannon/Electricity",6)
		GA1F A 0 A_JumpIfInventory("Zoomed",1,"ZoomedFire")
		GA2F A 0 A_JumpIfInventory("SiegeGauss",1,3)
		GA1F A 0 A_JumpIfInventory("PreciseGauss",1,2)
		GAUF A 0
		"####" ABCD 1 BRIGHT
		"####" A 0 A_PlaySound("D4GaussCannon/Fire",1)
		"####" A 0 A_JumpIfInventory("IsCrouching",1,3)
		"####" A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,40)
		"####" A 0 A_Jump(256,2)
		"####" A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,22)
		"####" A 0 A_AlertMonsters
		"####" A 0 A_RailAttack(250,2,1,None,None,RGF_FULLBRIGHT|RGF_SILENT,0,"GaussCannonProjectilePuff",0,0,0,0,5,0,"GaussCannonProjectileTrail",-5)
		"####" A 0 A_GunFlash("RecoilFlash")
		"####" E 1 BRIGHT
		"####" F 1 BRIGHT
		"####" A 0 A_StopSound(6)
		"####" GGHIIJKKKKKKLLMNPOO 1 A_WeaponReady(WRF_NOFIRE)
		GA2G A 0 A_JumpIfInventory("SiegeGauss",1,3)
		GA1G A 0 A_JumpIfInventory("PreciseGauss",1,2)
		GAUG A 0
		"####" A 15
		Goto Ready
	ZoomedFire:
		TNT1 A 0 A_TakeInventory("PreciseCharger",0)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,22)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash("ZoomedLVL")
		GA1G D 20
		Goto Ready2

	ZoomedLVL:
		TNT1 A 0 A_JumpIfInventory("PreciseCharger",100,"ZoomedVeryHigh")
		TNT1 A 0 A_JumpIfInventory("PreciseCharger",75,"ZoomedHigh")
		TNT1 A 0 A_JumpIfInventory("PreciseCharger",50,"ZoomedMiddle")
		TNT1 A 0 A_JumpIfInventory("PreciseCharger",25,"ZoomedLow")
		Goto ZoomedVeryLow

	ZoomedVeryHigh:
		TNT1 A 0 A_RailAttack(1024,2,1,None,None,RGF_FULLBRIGHT|RGF_SILENT,0,"GaussCannonProjectilePuff",0,0,0,0,5,0,"GaussCannonProjectileTrail",-5)
		Goto ResumeZoomedFireFrame
	ZoomedHigh:
		TNT1 A 0 A_RailAttack(512,2,1,None,None,RGF_FULLBRIGHT|RGF_SILENT,0,"GaussCannonProjectilePuff",0,0,0,0,5,0,"GaussCannonProjectileTrail",-5)
		Goto ResumeZoomedFireFrame
	ZoomedMiddle:
		TNT1 A 0 A_RailAttack(256,2,1,None,None,RGF_FULLBRIGHT|RGF_SILENT,0,"GaussCannonProjectilePuff",0,0,0,0,5,0,"GaussCannonProjectileTrail",-5)
		Goto ResumeZoomedFireFrame
	ZoomedLow:
		TNT1 A 0 A_RailAttack(128,2,1,None,None,RGF_FULLBRIGHT|RGF_SILENT,0,"GaussCannonProjectilePuff",0,0,0,0,5,0,"GaussCannonProjectileTrail",-5)
		Goto ResumeZoomedFireFrame
	ZoomedVeryLow:
		TNT1 A 0 A_RailAttack(64,2,1,None,None,RGF_FULLBRIGHT|RGF_SILENT,0,"GaussCannonProjectilePuff",0,0,0,0,5,0,"GaussCannonProjectileTrail",-5)
		Goto ResumeZoomedFireFrame

	ResumeZoomedFireFrame:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory("PreciseCharger",0)
		TNT1 A 0 A_StopSound(CHAN_ITEM)		
		TNT1 A 0 A_PlaySound("D4GaussCannon/ChargeFire2",CHAN_WEAPON)
		TNT1 A 0 A_SetBlend("Cyan",0.15,15)
		TNT1 A 0 A_SetPitch(pitch-2.5)
		TNT1 A 0 A_ZoomFactor(2.0)
		TNT1 A 2
		TNT1 A 0 A_ZoomFactor(2.5)
		TNT1 A 3
		TNT1 AAAAA 1 A_SetPitch(pitch+0.5)
		Stop

	AltFire:
		TNT1 A 0 A_JumpIfInventory("SiegeGauss",1,"SiegeAltFire")
		TNT1 A 0 A_JumpIfInventory("PreciseGauss",1,"PreciseAltFire")
		Goto AboutUpgrade
	PreciseAltFire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"StopPreciseAltFire")
		GA1G BCD 1
		TNT1 A 0 A_ZoomFactor(2.5)
		TNT1 A 0 A_GiveInventory("Zoomed",1)
		Goto Ready2
	StopPreciseAltFire:
		TNT1 A 0 A_TakeInventory("Zoomed",1)
		TNT1 A 0 A_TakeInventory("PreciseCharger",0)
		TNT1 A 0 A_StopSound(CHAN_ITEM)
		TNT1 A 0 A_ZoomFactor(1.0)
		GA1G BCD 1
		Goto Ready
	RecoilFlash:
		TNT1 AA 1 A_SetAngle(random(4,-4) + angle)
		TNT1 AAAAA 1 A_SetAngle(random(1,-1) + angle)
		Stop
	NoAmmo:
		TNT1 A 0 A_Print("You don't have enough ammo to use this weapon's alt fire",2)
		Goto Ready
	}
}

Actor GaussCannonShot : FastProjectile
{
scale 0.9
damage(random(40,50))
speed 190
height 8
radius 8
Alpha 0.9
RenderStyle Add
damagetype "Plasma"
Decal "Scorch"
+BLOODSPLATTER
+RIPPER
+FORCERADIUSDMG
+EXTREMEDEATH
-DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_SpawnItemEx("gaussheavyplasmaTracerTrail",0,0,2)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail2",(2 *momx)/-35.0,-(2 *momy)/-35.0,2+(2 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail3",(4 *momx)/-35.0,-(4 *momy)/-35.0,2+(4 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail4",(6 *momx)/-35.0,-(6 *momy)/-35.0,2+(6 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail5",(8 *momx)/-35.0,-(8 *momy)/-35.0,2+(8 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail6",(10*momx)/-35.0,-(10*momy)/-35.0,2+(10*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail7",(12*momx)/-35.0,-(12*momy)/-35.0,2+(12*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail8",(14*momx)/-35.0,-(14*momy)/-35.0,2+(14*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail9",(16*momx)/-35.0,-(16*momy)/-35.0,2+(16*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail10",(18*momx)/-35.0,-(18*momy)/-35.0,2+(18*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail11",(20*momx)/-35.0,-(20*momy)/-35.0,2+(20*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail12",(22*momx)/-35.0,-(22*momy)/-35.0,2+(22*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))
//		TNT1 A 0 A_CustomMissile("BluePlasmaFireNonStatic",2,0,random(0,360),2,random(0,180))
		TNT1 AAA 0 A_CustomMissile("BlueplasmaParticle",0,0,random(0,360))
		Loop
	Death:
		TNT1 AAAAA 0 A_SpawnItem("BlueFlare")
//		TNT1 AAAAA 0 A_CustomMissile("BluePlasmaFireNonStatic",2,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("BlueplasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
//		TNT1 A 0 A_SpawnItemEx("UnderwaterExplosion")
		TNT1 A 0 A_PlaySound("FAREXPL",3)
		TNT1 A 0 A_SpawnItemEx("RocketExplosion",0,0,13)
		TNT1 AAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		BL1I ABCD 1 BRIGHT A_SpawnItem("BlueFlare")
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		TNT1 A 1 A_Explode(200,100)
		Stop
	}
}

Actor GaussBomb : FastProjectile
{
scale 0.9
damage 50
speed 90
height 8
radius 8
gravity 1
Alpha 0.9
RenderStyle Add
damagetype Explosive	//plasma
Decal "Scorch"
+BLOODSPLATTER
+FORCERADIUSDMG
-DONTSPLASH
-nogravity
+EXTREMEDEATH
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_SpawnItemEx("gaussheavyplasmaTracerTrail",0,0,2)
		TNT1 AAA 0 A_CustomMissile("BlueplasmaParticle",0,0,random(0,360))
		Loop
	Death:
		TNT1 AAAAA 0 A_SpawnItemEx("BlueFlare")
//		TNT1 AAAAA 0 A_CustomMissile("BluePlasmaFireNonStatic",2,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("BlueplasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_PlaySound("FAREXPL",3)
		TNT1 AAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
		BL1I ABCD 1 BRIGHT A_SpawnItem("BlueFlare")
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		TNT1 A 1 A_Explode(300,180)
		Stop
	}
}

actor gaussheavyplasmaTracerTrail
{
Scale 0.4
Alpha 0.9
RenderStyle Add
+NOINTERACTION
+CLIENTSIDEONLY
	States
	{
	Spawn:
		SPKB A 2 BRIGHT
		stop
	}
}

actor gaussheavyplasmaTracerTrail2 : gaussheavyplasmaTracerTrail	{Alpha 0.85}
actor gaussheavyplasmaTracerTrail3 : gaussheavyplasmaTracerTrail	{Alpha 0.80}
actor gaussheavyplasmaTracerTrail4 : gaussheavyplasmaTracerTrail	{Alpha 0.75}
actor gaussheavyplasmaTracerTrail5 : gaussheavyplasmaTracerTrail	{Alpha 0.70}
actor gaussheavyplasmaTracerTrail6 : gaussheavyplasmaTracerTrail	{Alpha 0.65}
actor gaussheavyplasmaTracerTrail7 : gaussheavyplasmaTracerTrail	{Alpha 0.60}
actor gaussheavyplasmaTracerTrail8 : gaussheavyplasmaTracerTrail	{Alpha 0.55}
actor gaussheavyplasmaTracerTrail9 : gaussheavyplasmaTracerTrail	{Alpha 0.50}
actor gaussheavyplasmaTracerTrail10 : gaussheavyplasmaTracerTrail	{Alpha 0.45}
actor gaussheavyplasmaTracerTrail11 : gaussheavyplasmaTracerTrail	{Alpha 0.40}
actor gaussheavyplasmaTracerTrail12 : gaussheavyplasmaTracerTrail	{Alpha 0.35}
actor gaussheavyplasmaTracerTrail13 : gaussheavyplasmaTracerTrail	{Alpha 0.30}
actor gaussheavyplasmaTracerTrail14 : gaussheavyplasmaTracerTrail	{Alpha 0.25}
actor gaussheavyplasmaTracerTrail15 : gaussheavyplasmaTracerTrail	{Alpha 0.20}
actor gaussheavyplasmaTracerTrail16 : gaussheavyplasmaTracerTrail	{Alpha 0.15}
actor gaussheavyplasmaTracerTrail17 : gaussheavyplasmaTracerTrail	{Alpha 0.10}
actor gaussheavyplasmaTracerTrail18 : gaussheavyplasmaTracerTrail	{Alpha 0.05}

ACTOR GaussRailPuff : HitPuff
{
DamageType Kick
Decal "SmallerScorch"
-NOEXTREMEDEATH
+EXTREMEDEATH
+ALWAYSPUFF
+FORCEDECAL
	states
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("GaussRicoChet",0,0,-5)
		//Intentional fall-through
	Melee:
		TNT1 A 1
		stop
	}
}

ACTOR GaussRicoChet : RicoChet
{
	states
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterSmall")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterSmall")
		TNT1 A 0 A_SpawnItemEx("UnderwaterHitPuff")
		TNT1 A 1
		Stop
	}
}