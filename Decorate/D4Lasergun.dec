Actor D4Lasergun : BrutalD464Weapon Replaces LasergunSpawner
{
Scale 0.9
Weapon.SelectionOrder 200
Inventory.PickupSound "D4LightningGun/Pickup"
Inventory.PickupMessage "You got the Lightning Gun"
Weapon.AmmoType "Cell"
Weapon.AmmoUse 1
Weapon.AmmoGive 40
Weapon.BobStyle InverseSmooth
Weapon.BobRangeX 0.5
Weapon.BobRangeY 0.3
Weapon.BobSpeed 1.7
Inventory.Icon D4W9A0
Tag "Lightning Gun"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Spawn:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		D4W9 A -1
		Stop
	KickingFlash:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		D4LS CBA 1
		D4LS A 10
		D4LS ABC 1
		Stop
	AirKickingFlash:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		D4LS CBA 1
		D4LS A 12
		D4LS ABC 1
		Stop
	SlideKickingStart:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		D4LS CBA 1
		D4LS A 19
	SlideKickingEnd:
		D4LS A 7
		D4LS ABC 1
		Stop
	v1Ready:
	Ready:
		TNT1 A 0 A_PlaySound("D4LightningGun/Idle",CHAN_WEAPON,1,TRUE)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("WeaponAltMode",1,"NoAltMode")
		D4LG ABC 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("D4PeaGunSelected",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		TNT1 A 0 A_TakeInventory("IsD4Weapon",0)
		TNT1 A 0 A_TakeInventory("D4AltMode",0)
		TNT1 A 0 A_Takeinventory("HasPlasmaWeapon",0)
		D4LS CBA 1
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_GiveInventory("D4PeaGunSelected",1)
		TNT1 A 0 A_Giveinventory("HasPlasmaWeapon",1)
		TNT1 A 0 A_GiveInventory("IsD4Weapon",1)
		D4LS ABC 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_ZoomFactor(0.99)
		D4LF D 1 A_PlaySound("D4LightningGun/Start",CHAN_VOICE)
		D4LF C 1 A_FireCustomMissile("AlphaLZRGkerLaser",0,1,6)
	Hold:
		TNT1 A 0 A_PlaySound("D4LightningGun/Loop",CHAN_WEAPON,1,TRUE)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle4",20,5,22)
		D4LF A 1 Bright A_FireCustomMissile("AlphaLZRGkerLaser",0,0,6)
		D4LF B 1 Bright A_FireCustomMissile("AlphaLZRGkerLaser",0,1,6)
		D4LF A 1 Bright A_FireCustomMissile("AlphaLZRGkerLaser",0,0,6)
		TNT1 A 0 A_Refire
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		D4LF C 1 A_PlaySound("D4LightningGun/Stop",CHAN_VOICE)
		TNT1 A 0 A_ZoomFactor(1.0)
		D4LF D 1 A_FireCustomMissile("GunFireSmoke",0,0,6)
		D4LG A 2
		Goto Ready
	UpgradeWeapons:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("IsUpgradingWeapons",1)
		TNT1 A 0 SetPlayerProperty(0,1,4)
		PK4P ABCDEFGH 2
		TNT1 A 6
		TNT1 A 0 A_PlaySound("UACBot/Pickup")
		TNT1 A 0 SetPlayerProperty(0,0,4)
		TNT1 A 12
		Goto Select
	NoAltMode:
		TNT1 A 0 A_TakeInventory("WeaponAltMode",1)
		TNT1 A 0 A_Print("UAC Modpacks do not apply to the Lightning Gun.",2)
		Goto Ready
	}
}